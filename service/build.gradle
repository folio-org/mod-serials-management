buildscript {
  repositories {
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://repo.grails.org/artifactory/core" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    classpath "org.grails.plugins:views-gradle:2.0.4"
    classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.0"
    classpath "org.grails.plugins:hibernate5:${gormHibernate-".RELEASE"}"
    classpath 'org.grails.plugins:database-migration:3.1.0'
    classpath 'com.bmuschko:gradle-docker-plugin:6.6.1'
    classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.4.0.2513'
  }
}




final String version_regex = /^(((\d+)\.\d+)\.\d+)(-([a-zA-Z]+(\.\d+)?))?$/
Map<String, String> env = System.getenv();
String calculated_version = null;

// If we are building in a Jenkins environment append the build number onto the snapshot tag
if ( ( appVersion.contains('SNAPSHOT') ) && ( env.BUILD_NUMBER != null ) ) {
  println("Detected snapshot version and build number - appending");
  calculated_version = "${appVersion}.${env.BUILD_NUMBER}".toString()
}
else {
  println("Release version");
  calculated_version = "${appVersion}".toString()
}

version "${calculated_version}"
group 'org.olf'

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.grails.grails-web'
apply plugin: 'org.grails.plugins.views-json'
apply plugin: 'com.github.erdi.webdriver-binaries'
apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'


java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

eclipse {
  autoBuildTasks ([':processResources'])
  synchronizationTasks ([':processResources'])
  
  classpath {
    defaultOutputDir = new File(project.buildDir.canonicalPath, 'main')
    file {
      
      whenMerged { classpath ->
        
        Set<String> resources = sourceSets.main?.resources?.srcDirs?.collect { it.canonicalPath }
        
        entries.collect().each {
          
          if (it.path =='grails-app/conf' && it.output?.toLowerCase().endsWith('test')) {
            classpath.entries.remove( it )
            println "Removed ${it}"
            return
          }

          if (it.hasProperty('output') && it.output.startsWith('bin/')) {
            it.output = 'build/' + it.output.substring(4)
          }
          
          // Eclude the output of the resources added to the source sets for grails.
          // Instead we add the results of the 'processResources' tasks to the classpath later.
          if (it.hasProperty('dir') && resources.find { res -> it.dir?.canonicalPath?.startsWith(res) } ) {
            it.exported = false
            it.excludes << '**/*'
          }
        }
        def lib = new org.gradle.plugins.ide.eclipse.model.Library(fileReference(file("${tasks.processResources.destinationDir}")))
        println "Adding: ${lib}"
        entries << lib
      }
    }
  }
}

repositories {
  mavenCentral()
  maven { url 'https://repo.grails.org/grails/core' }
  maven { url 'https://maven.indexdata.com/' }
  maven { url "https://maven.k-int.com/content/repositories/public" }
  maven { url "https://repo.gradle.org/gradle/libs-releases-local" }
}

sourceSets {
  main {
    resources {
      srcDir 'grails-app/migrations'
      srcDir 'src/main/okapi/tenant'
    }
  }
}

dependencies {

  compile "org.springframework.boot:spring-boot-starter-logging"
  compile "org.springframework.boot:spring-boot-autoconfigure"
  compile "org.grails:grails-core"
  compile "org.springframework.boot:spring-boot-starter-actuator"
  compile "org.grails:grails-plugin-url-mappings"
  compile "org.grails:grails-plugin-rest"
  compile "org.grails:grails-plugin-codecs"
  compile "org.grails:grails-plugin-interceptors"
  compile "org.grails:grails-plugin-services"
  compile "org.grails:grails-plugin-datasource"
  compile "org.grails:grails-plugin-databinding"
  compile "org.grails:grails-web-boot"
  compile "org.grails:grails-logging"
  compile "org.grails.plugins:async"
  compile "org.grails.plugins:views-json"
  compile "org.grails.plugins:views-json-templates"
  compile "org.grails.plugins:events"
  compile "org.grails.plugins:hibernate5"
  compileOnly "io.micronaut:micronaut-inject-groovy"

  console ("org.grails:grails-console") {
    // The gpars transient depends on an older version of groovy-all.
    exclude group: 'org.codehaus.groovy', module: 'groovy'
  }

  profile "org.grails.profiles:rest-api"
  // runtime "com.h2database:h2"
  runtime "javax.xml.bind:jaxb-api:2.3.1"

  profile "org.grails.profiles:rest-api"
  runtime "com.zaxxer:HikariCP:3.2.0"

  // Finance
  compile "javax.money:money-api:1.1"
  compile "org.javamoney:moneta:1.1"
  

  /************** Testing ************/
  testCompile "org.grails:grails-gorm-testing-support"

  testCompile "org.mockito:mockito-core"
  testCompile "org.grails:grails-web-testing-support"
  testCompile "org.grails.plugins:geb"
  //  testCompile "io.micronaut:micronaut-http-client"
  testCompile "org.seleniumhq.selenium:selenium-remote-driver:3.14.0"
  testCompile "org.seleniumhq.selenium:selenium-api:3.14.0"
  testCompile "org.seleniumhq.selenium:selenium-support:3.14.0"
  testRuntime "org.seleniumhq.selenium:selenium-chrome-driver:3.14.0"
  testRuntime "org.seleniumhq.selenium:selenium-firefox-driver:3.14.0"
  compile 'org.z3950.zing:cql-java:1.13'


  compile "org.springframework.boot:spring-boot-starter-undertow" // Replaces spring-boot-starter-tomcat
  compile "org.hibernate:hibernate-core:5.4.19.Final"             // Update to latest 5.4
  compile "org.hibernate:hibernate-java8:5.4.19.Final"
  runtime "com.zaxxer:HikariCP:3.4.5"                             // Replaces Tomcat JDBC pool
  runtime "org.postgresql:postgresql:42.3.1"

  compile ('org.grails.plugins:database-migration:3.1.0') {       // Required by Grails Okapi
    exclude group: 'org.liquibase', module: 'liquibase-core'
  }
  compile 'org.liquibase:liquibase-core:3.9.0'

  compile 'com.opencsv:opencsv:4.6'
  compile 'commons-io:commons-io:2.6'

  // compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'     // TODO: Migrate away from this resource.
  
  compile 'com.k_int.grails:web-toolkit-ce:6.4.1'
  compile 'com.k_int.okapi:grails-okapi:5.0.1'

  // Custom non profile deps.
  compile 'org.apache.kafka:kafka-clients:2.3.0'
  runtime "org.grails.plugins:async"
  runtime "org.grails.plugins:events"
  compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
  
  // Better test reports.
  testCompile( 'com.athaydes:spock-reports:1.6.1' ) {
    transitive = false // this avoids affecting your version of Groovy/Spock
  }
}

bootRun {
  ignoreExitValue true
  jvmArgs(
      '-Dspring.output.ansi.enabled=always',
      '-server',
      '-noverify',
      '-XX:TieredStopAtLevel=1',
      '-Xmx1024m')
  sourceResources sourceSets.main
  String springProfilesActive = 'spring.profiles.active'
  systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    systemProperty "webdriver.chrome.driver", System.getProperty('webdriver.chrome.driver')
    systemProperty "webdriver.gecko.driver", System.getProperty('webdriver.gecko.driver')
}

task generateDescriptors(type: Copy) {

  dependsOn buildProperties

  File grailsBuildInfoFile = buildProperties.outputs.files.files.find { it.name == 'grails.build.info' }

  final def props = [:]

  if(!(grailsBuildInfoFile)) return // No need to continue if the file is not there

  doFirst {

    // Place the loading within a do first as the info file from the buildProperties
    // task will not be present until it is executed. This owuld cause a file not found
    // during task configureation otherwise.
    Properties properties = new Properties()

    // Read properties from the file
    grailsBuildInfoFile.withInputStream {
      properties.load(it)
    }

    // Need to parse multi-dimensional keys.
    ConfigSlurper cs = new ConfigSlurper()
    props.putAll(cs.parse(properties))
  }

  from ('./src/main/okapi') {
    include '*-template.json'
  }
  into './build/resources/main/okapi'
  filteringCharset = 'UTF-8'
  expand(props)
  rename { String fileName ->
    fileName.replace('-template', '')
  }
}

buildProperties.doLast {

  // Find the right file
  File grailsBuildInfoFile = it.outputs.files.files.find { it.name == 'grails.build.info' }

  if(!grailsBuildInfoFile) {
    System.err.println("Unable to locate grails.build.info");
    return // No need to continue if the file is not there
  }

  Properties properties = new Properties()

  // Read properties from the file
  grailsBuildInfoFile.withInputStream {
    properties.load(it)
  }

  // Load the app version
  String version = properties.getProperty('info.app.version')
  def versionParts
  if (version && (versionParts = version =~ version_regex)) {
    println("Version parts : ${versionParts[0]}");
    versionParts[0].with {
      properties.setProperty('info.app.majorVersion', it[3])
      properties.setProperty('info.app.minorVersion', it[2])
      properties.setProperty('info.app.patchVersion', it[1])

      if (it[5]) {
        properties.setProperty('info.app.testVersion', it[5])
      }

      println("version properties: ${properties}");
    }

    version.replaceAll(/^(((\d+)\.\d+)\.\d+)(-([\w]+))?$/ , '$1')
  }

  if(env.BUILD_NUMBER)
    properties.setProperty('build.number', env.BUILD_NUMBER)

  if ( env.GIT_REVISION )
    properties.setProperty('build.git.revision', env.GIT_REVISION)

  if ( env.GIT_BRANCH )
    properties.setProperty('build.git.branch', env.GIT_BRANCH)

  if (env.GIT_COMMIT)
    properties.setProperty('build.git.commit', env.GIT_COMMIT)

  // Add new properties from various sources
  properties.setProperty('build.time', new Date().format("yyyy-MM-dd HH:mm:ss"))

  properties.setProperty('build.host', InetAddress.localHost.hostName)

  // Get a System property
  properties.setProperty('build.java.version', System.getProperty('java.version'))

  // Write the properties back to the file
  grailsBuildInfoFile.withOutputStream {
    properties.store(it,null)
  }
}

processResources.dependsOn generateDescriptors

/**
 * Docker build config.
 */
import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

ext {
  dockerId = "${project.dockerRepo}/${project.name}:${project.version}${dockerTagSuffix}"
  dockerBuildDir = mkdir("${buildDir}/docker")
}

task prepareDocker(type: Copy, dependsOn: assemble) {
  description = 'Copy files to build directory'
  group = 'Docker'

  from project.jar

  // from otherfileLoc

  into dockerBuildDir
}

task createDockerfile(type: Dockerfile, dependsOn: prepareDocker) {
  description = 'Create a Dockerfile file'
  group = 'Docker'

  destFile = project.file("${dockerBuildDir}/Dockerfile")

  from 'folioci/alpine-jre-openjdk11:latest'
  instruction 'LABEL maintainer="Knowledge Integration"'

  exposePort 8080

  // Copying straight into the default directory should be enough
  copyFile jar.archiveName, '/usr/verticles/application.jar'
}

task buildImage(type: DockerBuildImage, dependsOn: createDockerfile) {
  description = 'Create Docker image to run the Grails application'
  group = 'Docker'

  inputDir = file(dockerBuildDir)
  images.add(dockerId)
}


tasks.withType(GroovyCompile) {
  configure(groovyOptions) {
    forkOptions.jvmArgs = ['-Xmx1024m']
  }
}

webdriverBinaries {
  chromedriver '2.45.0'
  geckodriver '0.24.0'
}

tasks.withType(Test) {
  systemProperty "geb.env", System.getProperty('geb.env')
  systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
  systemProperty "webdriver.chrome.driver", System.getProperty('webdriver.chrome.driver')
  systemProperty "webdriver.gecko.driver", System.getProperty('webdriver.gecko.driver')
}


jacoco {
  toolVersion = "0.8.8"
}


// Use ./gradlew clean build jacocoTestReport
jacocoTestReport {
  // See https://github.com/grails/grails-core/issues/11318
  // executionData test, integrationTest
  executionData = files tasks.withType(Test)*.jacoco*.destinationFile.findAll { it.exists() }
  // executionData = files tasks.withType(Test)*.jacoco*.destinationFile.findAll { it.exists() } tasks.withType(IntegrationTest)*.jacoco*.destinationFile.findAll { it.exists() }

  println("execution data : ${executionData}");
  reports {
    xml.enabled true
    html.enabled true
    csv.enabled true
  }
  afterEvaluate {
    classDirectories = files(classDirectories.files.collect {
        fileTree(dir: it,
                 exclude: ['**/Application**',
                           '**/BootStrap**',
                           '**/**gson**',
                           '**/**closure**',
                           '**/UrlMappings**'])
    })
  }
  dependsOn test, integrationTest
}

sonarqube {
    properties {
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.projectKey", "org.folio:mod-serials-management"
        property "sonar.verbose", true
        property "sonar.projectName", "mod-serials-management"
        property "sonar.coverage.jacoco.xmlReportPaths", "${rootDir}/build/reports/jacoco/test/jacocoTestReport.xml"
    }
}
